import java.text.DecimalFormat

plugins {
    id 'com.android.application'
    id 'org.greenrobot.greendao'
    id 'androidx.navigation.safeargs'
    id 'com.google.firebase.crashlytics'
    id 'com.google.gms.google-services'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "ru.mobnius.cic"
        minSdkVersion 22
        targetSdkVersion 32
        versionCode 5
        versionName getVersion()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", getVersion())
        resConfigs "RU"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'consumer-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    lint {
        abortOnError true
        checkReleaseBuilds true
        enable 'StopShip'
        fatal 'StopShip'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

}


configurations {
    all {
        exclude group: 'org.json', module: 'json'
        exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel-ktx'
        exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel:2.3.1'
        exclude group: 'org.apache.commons', module: 'commons-lang3'
    }
}

greendao {
    schemaVersion 1
}

repositories {
    maven {
        url "https://maven.google.com/"
        url 'https://developer.huawei.com/repo/'
    }
}

dependencies {
    implementation 'com.google.dagger:dagger:2.42'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.42'
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.5.0"
    implementation "androidx.lifecycle:lifecycle-livedata:2.5.0"
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.navigation:navigation-fragment:2.5.0"
    implementation "androidx.navigation:navigation-ui:2.5.0"
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'com.github.bumptech.glide:glide:4.13.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.13.0'
    implementation platform('com.google.firebase:firebase-bom:29.1.0')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.huawei.hms:base:4.0.2.300'
    implementation 'androidx.work:work-runtime-ktx:2.7.1'

    implementation 'org.osmdroid:osmdroid-android:6.1.13'
    implementation 'com.github.MKergall:osmbonuspack:6.6.0'

    implementation 'com.google.android.gms:play-services-location:20.0.0'
    implementation 'com.huawei.hms:base:4.0.2.300'

    implementation 'com.google.code.gson:gson:2.9.0'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':simple-core')

    testImplementation 'org.mockito:mockito-core:3.10.0'
    androidTestImplementation 'org.mockito:mockito-android:3.10.0'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.robolectric:robolectric:4.8.1"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    androidTestImplementation "androidx.navigation:navigation-testing:2.5.0"
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation ('androidx.test.espresso:espresso-contrib:3.4.0') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
    debugImplementation "androidx.fragment:fragment-testing:1.5.0"

}

static def startOfDay() {
    def now = new Date().getTime()
    def cal = Calendar.getInstance()
    cal.set(2022, 01, 21, 0, 0, 0)
    def start = cal.getTimeInMillis()

    return new DecimalFormat("#").format(Math.floor(Math.abs(now - start) / (1000 * 3600 * 24)))
}

static def minuteOfDay() {
    def now = new Date()
    return (now.format("HH").toInteger() * 60) + now.format("mm").toInteger()
}

static def getRelease() {
    /**
     * 0 – альфа (прототип);
     * 1 – бета (бета);
     * 2 – выпуск-кандидат (передан на тестирование заказчику);
     * 3 – публичный выпуск (окончательно передан заказчику).
     */
    return 1
}

static def getVersion() {
    return "4." + startOfDay() + "." + getRelease() + "." + minuteOfDay()
}
